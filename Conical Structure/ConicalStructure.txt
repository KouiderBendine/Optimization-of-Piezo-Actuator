FINISH
/CLEAR
/CWD,'D:\Vibration_control_Paper\Ansys_Code\Canonical_Patch'
/PREP7

!DIMENSIONS
!**********************************************************
*use,Iter_param.inp

RaiSeg    = YC
*AFUN, DEG
ALfaN  = 15.0
Alfa=TAN(ALfaN)
Length          = 400     ! Length
Raidus1         = 100     ! Small Raidus
Raidus2         = Alfa*Length + Raidus1  
Thickness       = 1       ! Thickness

ELsize          = 20      ! Element Size


Patch_width     = 50      ! Width Piezo    
Patch_Thickness = 0.5     ! Thickness 
Nseg            = 18      ! Number of segments
hseg            = 8       ! Number of segments


YCoor = RaiSeg*(360/Nseg)
ZCoor = ZC!4


*DIM,LengthPatch,ARRAY,4 
 j=0    
*Do,i,ZCoor*Patch_width,(ZCoor+1)*Patch_width,Patch_width
  j= j +1
  LengthPatch(j)  = Alfa*i+(Raidus1+Thickness)
*Enddo

!ELEMENT TYPE
ET,1,SOLID186
ET,2,SOLID5,3  

!MATERIAL PROPERTIES
!**********************************************************

MPTEMP,1,0
MPDATA,EX,1,,210E3
MPDATA,PRXY,1,,0.33
MPDATA,DENS,1,,7.85E-9

!**********************************************************
!MATERIAL2: PZT

TB, ANEL, 2 , 1 , 0
TBDATA, 1, 9.6903E+04 , 7.0017E+04 , 7.0017E+04
TBDATA, 7, 1.2276E+05 , 7.6462E+04
TBDATA, 12, 1.2276E+05
TBDATA, 16, 2.2272E+04
TBDATA, 19, 2.3148E+04
TBDATA, 21, 2.2272E+04
TB, PIEZ, 2 

TBDATA, 1, 0.0138
TBDATA, 4, -0.0071
TBDATA, 7, -0.0071
TBDATA, 11, 0.0119
TBDATA, 18, 0.0119
EMUNIT, EPZRO, 8.85E-12
MP, PERX, 2 , 857
MP, PERY, 2 , 929
MP, PERZ, 2 , 929
MP, DENS, 2 , 7.8E-09

!Ring GEOMETRY
!**********************************************************
CSYS,1
K,1,Raidus1,0,0
K,2,Raidus1+Thickness,0,0

K,3,Raidus2,0,Length
K,4,Raidus2+Thickness,0,Length

K,5,0,0,0
K,6,0,0,Length

LSTR,1,2
LSTR,3,4
LSTR,2,4
LSTR,1,3
AL,ALL
VROTAT,1,,,,,,5,6,360,Nseg,

!Patch Geometry
!**********************************************************
Allsel,all

*GET,KpMax,kp,0,NUM,MAX 
K,KpMax+1,LengthPatch(1),YCoor,ZCoor*Patch_width
K,KpMax+2,LengthPatch(1)+Patch_Thickness,YCoor,ZCoor*Patch_width

K,KpMax+3,LengthPatch(2),YCoor,(ZCoor+1)*Patch_width
K,KpMax+4,LengthPatch(2)+Patch_Thickness,YCoor,(ZCoor+1)*Patch_width

LSTR,KpMax+1,KpMax+2
LSTR,KpMax+3,KpMax+4
LSTR,KpMax+2,KpMax+4
LSTR,KpMax+1,KpMax+3

allsel,all
*GET,LMax,Line,0,NUM,MAX 
Lsel,s,Line,,LMax-3,LMax
AL,ALL

*GET,AMax,area,0,NUM,MAX 
VROTAT,AMax,,,,,,5,6,360/Nseg,1,

Vsel,all

!**********************************************************
*GET,VMax,Volum,0,NUM,MAX 
Vsel,s,Volu,,VMax
CM,Piezovol,VOLU
vsel,all

!**********************************************************
!*** Assign Material to piezo
Vsel,S,VOLU,,Piezovol
VATT,2,,2,,
Vsel,all

!**********************************************************
!*** Assign Material to the Ring
Vsel,U,VOLU,,Piezovol
CM,Ringvol,VOLU
VATT,1,,1,,
vsel,all
Aovlap,all

*DO,i,1,hseg-1
wpof,,,Patch_width
VSBW,ALL
*ENDDO
WPAVE,0,0,0
vsel,all
Aovlap,all
vglue,all
!**********************************************************
!*** Meshing
ALLSEL,ALL
ESIZE,ELsize
VSWEEP,ALL

!Define Botom and Top electrodes of piez
!----------------------
Asel,s,Area,,95
   Nsla,s,1
*Get,Botom,node,0,num,min
  Cp,1,volt,all
  D,Botom,VOLT,0,0
allsel

Asel,s,Area,,93
   Nsla,s,1
d,all,volt,0,0                  ! Ground bottom electrode,
  nsel,all
allsel

!Asel,s,Area,,93
!   Nsla,s,1
!*Get,ANTOP,node,0,num,min
!  Cp,2,volt,all
!  D,ANTOP,VOLT,0,0
!allsel


!CSYS,0
Nsel,s,loc,z,0,Patch_width-Patch_width/5                  ! Clamp left end 
d,all,ux,0,,,,uy,uz		! Defines DOF constraints, clamp the end
nsel,all
allsel,all

acel,0,-9.81,0
finish

*use,State_Space_Conic_one.mac
